"EBNF defined in itself"
{
	syntax     = [ title ] "{" { production } "}" .
	title      = literal .
	production = identifier "=" expression "." | identifier ":" expression ";" .
	expression = term { "|" term } .
	term       = factor { factor } .
	factor     = identifier | literal | optional | "(" expression ")" | repetition | onemore .
	optional   = "[" expression "]" | factor "?" .
	repetition = "{" expression "}" | factor "*" .
	onemore    = "<" expression [ "~" expression ] ">" | factor "+" .
	identifier = < `character` > .
	literal    = "'" < `character` > "'" | "\"" < `character` > "\"" | "`" < `character` > "`" .
	comment    = "//" { `character` } `newline` | "/*" { `character` } "*/" .
}
